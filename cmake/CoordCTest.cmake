include(CMakeParseArguments)

function(coord_add_fixture_daemon)
    set(options)
    set(oneValueArgs NAME FIXTURE LISTEN ROOT TLS_CA TLS_CERT TLS_KEY READY_FILE PID_FILE SHUTDOWN_TOKEN)
    cmake_parse_arguments(COORD "" "${oneValueArgs}" "" ${ARGN})
    if(NOT COORD_NAME)
        message(FATAL_ERROR "coord_add_fixture_daemon: NAME required")
    endif()
    set(fixture "${COORD_FIXTURE}")
    if(fixture STREQUAL "")
        set(fixture "${COORD_NAME}")
    endif()

    if(NOT COORD_LISTEN)
        if(WIN32)
            set(COORD_LISTEN "tcp://127.0.0.1:7777")
        else()
            set(COORD_LISTEN "unix://coordd.sock")
        endif()
    endif()
    if(NOT COORD_ROOT)
        set(COORD_ROOT "${CMAKE_CURRENT_BINARY_DIR}/coordd_root")
    endif()

    set(cmd $<TARGET_FILE:coordctl> daemonize --coordd $<TARGET_FILE:coordd> --listen ${COORD_LISTEN} --root ${COORD_ROOT})
    if(COORD_TLS_CA)
        list(APPEND cmd --tls-ca ${COORD_TLS_CA} --tls-cert ${COORD_TLS_CERT} --tls-key ${COORD_TLS_KEY})
    endif()
    if(COORD_READY_FILE)
        list(APPEND cmd --ready-file ${COORD_READY_FILE})
    endif()
    if(COORD_PID_FILE)
        list(APPEND cmd --pid-file ${COORD_PID_FILE})
    endif()
    if(COORD_SHUTDOWN_TOKEN)
        list(APPEND cmd --shutdown-token ${COORD_SHUTDOWN_TOKEN})
    endif()

    add_test(NAME ${COORD_NAME} COMMAND ${cmd})
    set_tests_properties(${COORD_NAME} PROPERTIES FIXTURES_SETUP ${fixture})
endfunction()

function(coord_add_session_test)
    set(options)
    set(oneValueArgs NAME SPEC CONNECT FIXTURE REPORT_DIR TLS_CA TLS_CERT TLS_KEY)
    cmake_parse_arguments(COORD "" "${oneValueArgs}" "" ${ARGN})
    if(NOT COORD_NAME)
        message(FATAL_ERROR "coord_add_session_test: NAME required")
    endif()
    if(NOT COORD_SPEC)
        message(FATAL_ERROR "coord_add_session_test: SPEC required")
    endif()

    set(cmd $<TARGET_FILE:coordctl> submit --spec ${COORD_SPEC})
    if(COORD_CONNECT)
        list(APPEND cmd --connect ${COORD_CONNECT})
    endif()
    if(COORD_REPORT_DIR)
        list(APPEND cmd --report ${COORD_REPORT_DIR})
    endif()
    if(COORD_TLS_CA)
        list(APPEND cmd --tls-ca ${COORD_TLS_CA} --tls-cert ${COORD_TLS_CERT} --tls-key ${COORD_TLS_KEY})
    endif()

    add_test(NAME ${COORD_NAME} COMMAND ${cmd})
    if(COORD_FIXTURE)
        set_tests_properties(${COORD_NAME} PROPERTIES FIXTURES_REQUIRED ${COORD_FIXTURE})
    endif()
endfunction()

function(coord_add_fixture_cleanup)
    set(oneValueArgs NAME CONNECT FIXTURE TOKEN TLS_CA TLS_CERT TLS_KEY)
    cmake_parse_arguments(COORD "" "${oneValueArgs}" "" ${ARGN})
    if(NOT COORD_NAME)
        message(FATAL_ERROR "coord_add_fixture_cleanup: NAME required")
    endif()
    set(cmd $<TARGET_FILE:coordctl> shutdown)
    if(COORD_CONNECT)
        list(APPEND cmd --connect ${COORD_CONNECT})
    endif()
    if(COORD_TOKEN)
        list(APPEND cmd --token ${COORD_TOKEN})
    endif()
    if(COORD_TLS_CA)
        list(APPEND cmd --tls-ca ${COORD_TLS_CA} --tls-cert ${COORD_TLS_CERT} --tls-key ${COORD_TLS_KEY})
    endif()
    add_test(NAME ${COORD_NAME} COMMAND ${cmd})
    if(COORD_FIXTURE)
        set_tests_properties(${COORD_NAME} PROPERTIES FIXTURES_CLEANUP ${COORD_FIXTURE})
    endif()
endfunction()
